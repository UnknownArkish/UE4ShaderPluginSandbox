#include "/Engine/Public/Platform.ush"
#include "FFT.ush"

uint MapSize;
float Time;
StructuredBuffer<Complex> H0Buffer;
RWTexture2D<float4> H0DebugTexture;

[numthreads(8, 8, 1)]
void DebugH0CS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	uint2 PixelCoord = DispatchThreadId;
	uint Index = PixelCoord.y * MapSize + PixelCoord.x; // Structured Buffer index corresponding wave number k

	const float SCALE = 100.0f; // デフォルトのパラメータ設定でおおよそ結果がきれいに見える値
    H0DebugTexture[PixelCoord] = float4(H0Buffer[Index].x * SCALE, H0Buffer[Index].y * SCALE, 0.0, 1.0);
}

StructuredBuffer<Complex> HtBuffer;
RWTexture2D<float4> HtDebugTexture;

[numthreads(8, 8, 1)]
void DebugHtCS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	uint2 PixelCoord = DispatchThreadId;
	uint Index = PixelCoord.y * MapSize + PixelCoord.x;

	const float SCALE = 100.0f; // デフォルトのパラメータ設定でおおよそ結果がきれいに見える値
	HtDebugTexture[PixelCoord] = float4(HtBuffer[Index].x * SCALE, HtBuffer[Index].y * SCALE, 0.0, 1.0);
}

StructuredBuffer<Complex> DkxBuffer;
RWTexture2D<float4> DkxDebugTexture;

[numthreads(8, 8, 1)]
void DebugDkxCS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	uint2 PixelCoord = DispatchThreadId;
	uint Index = PixelCoord.y * MapSize + PixelCoord.x;

	const float SCALE = 100.0f; // デフォルトのパラメータ設定でおおよそ結果がきれいに見える値
	DkxDebugTexture[PixelCoord] = float4(DkxBuffer[Index].x * SCALE, DkxBuffer[Index].y * SCALE, 0.0, 1.0);
}

StructuredBuffer<Complex> DkyBuffer;
RWTexture2D<float4> DkyDebugTexture;

[numthreads(8, 8, 1)]
void DebugDkyCS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	uint2 PixelCoord = DispatchThreadId;
	uint Index = PixelCoord.y * MapSize + PixelCoord.x;

	const float SCALE = 100.0f; // デフォルトのパラメータ設定でおおよそ結果がきれいに見える値
	DkyDebugTexture[PixelCoord] = float4(DkyBuffer[Index].x * SCALE, DkyBuffer[Index].y * SCALE, 0.0, 1.0);
}

StructuredBuffer<float> OmegaBuffer;
RWStructuredBuffer<Complex> OutHtBuffer;
RWStructuredBuffer<Complex> OutDkxBuffer;
RWStructuredBuffer<Complex> OutDkyBuffer;

[numthreads(8, 8, 1)]
void UpdateSpectrumCS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	uint2 PixelCoord = DispatchThreadId;
	uint Index = PixelCoord.y * MapSize + PixelCoord.x;
	uint MinusIndex = (MapSize - PixelCoord.y - 1) * MapSize + (MapSize - PixelCoord.x - 1);

	// H(k, 0)からのH(k, t)への時間発展。H(k, t) = H(k, 0) * e^(i * omega * t) + Conj(H(-k, 0)) * e^(-i * omega * t)
	Complex Hk0 = H0Buffer[Index];
	Complex Hminusk0 = H0Buffer[MinusIndex];
	float SinOmega, CosOmega;
	sincos(OmegaBuffer[Index] * Time, SinOmega, CosOmega);

	Complex Hkt;
	Hkt.x = (Hk0.x + Hminusk0.x) * CosOmega - (Hk0.y + Hminusk0.y) * SinOmega;
	Hkt.y = (Hk0.x - Hminusk0.x) * SinOmega + (Hk0.y - Hminusk0.y) * CosOmega;

	// H(k, t)からのD(k, t)の計算。D(k, t) = i * k / |k| * H(k, t)
	float2 K = PixelCoord - float2(MapSize * 0.5f, MapSize * 0.5f);
	float KLen = length(K);
	if (KLen < 1e-12f)
	{
		K = float2(0.0f, 0.0f);
	}
	else
	{
		K /= KLen;
	}

	// D(k, t)は波数空間のベクトルなので、kのx方向とy方向に分けて扱う
	Complex Dkxt = K.x * Complex(Hkt.y, -Hkt.x);
	Complex Dkyt = K.y * Complex(Hkt.y, -Hkt.x);

	OutHtBuffer[Index] = Hkt;
	OutDkxBuffer[Index] = Dkxt;
	OutDkyBuffer[Index] = Dkyt;
}

StructuredBuffer<Complex> InDkBuffer;
RWStructuredBuffer<Complex> FFTWorkBufferUAV; // TODO:SharedMemoryに入れるようにしたい
float ChoppyScale;
RWStructuredBuffer<float> OutDxBuffer;
RWStructuredBuffer<float> OutDyBuffer;
RWStructuredBuffer<float> OutDzBuffer;

void CopyComplexDataSrcToLocal(inout Complex LocalComplexBuffer[RADIX], in uint ScanIdx, uint Loc, uint Stride, uint Size, StructuredBuffer<Complex> SrcBuffer)
{
	for (uint i = 0; i < RADIX; ++i)
	{
		LocalComplexBuffer[i] = Complex(0.f, 0.f);
	}
	
	uint2 Pixel = uint2(Loc, ScanIdx);

	// Horizontal copy
	UNROLL
	for (uint i = 0; i < RADIX; ++i, Pixel.x += Stride)
	{
		uint Index = Pixel.y * Size + Pixel.x;
		LocalComplexBuffer[i] = SrcBuffer[Index];
	}
}

void CopyWorkBufferToLocal(inout Complex LocalComplexBuffer[RADIX], in uint ScanIdx, uint Loc, uint Stride, uint Size)
{
	for (uint i = 0; i < RADIX; ++i)
	{
		LocalComplexBuffer[i] = Complex(0.f, 0.f);
	}
	
	uint2 Pixel = uint2(ScanIdx, Loc);

	// Vertical copy
	UNROLL
	for (uint i = 0; i < RADIX; ++i, Pixel.y += Stride)
	{
		uint Index = Pixel.y * Size + Pixel.x;
		LocalComplexBuffer[i] = FFTWorkBufferUAV[Index];
	}
}

void CopyComplexDataLocalToWorkBuffer(in Complex LocalComplexBuffer[RADIX], uint ScanIdx, uint Loc, uint Stride, uint Size)
{
	uint2 Pixel = uint2(Loc, ScanIdx);

	// Horizontal copy
	UNROLL
	for (uint r = 0; r < RADIX && Pixel.x < Size; ++r, Pixel.x += Stride)
	{
		uint Index = Pixel.y * Size + Pixel.x;
		FFTWorkBufferUAV[Index] = LocalComplexBuffer[r];
	}
}

void CopyRealDataLocalToDxBuffer(in Complex LocalComplexBuffer[RADIX], uint ScanIdx, uint Loc, uint Stride, uint Size)
{
	uint2 Pixel = uint2(ScanIdx, Loc);
	// cos(pi * (m1 + m2))
	int SignCorrection = ((Pixel.x + Pixel.y) & 1) ? -1 : 1;

	// Vertical copy
	UNROLL
	for (uint r = 0; r < RADIX && Pixel.y < Size; ++r, Pixel.y += Stride)
	{
		uint Index = Pixel.y * Size + Pixel.x;
		OutDxBuffer[Index] = LocalComplexBuffer[r].x * SignCorrection * ChoppyScale; // 実数部のみコピー
	}
}
void CopyRealDataLocalToDyBuffer(in Complex LocalComplexBuffer[RADIX], uint ScanIdx, uint Loc, uint Stride, uint Size)
{
	uint2 Pixel = uint2(ScanIdx, Loc);
	// cos(pi * (m1 + m2))
	int SignCorrection = ((Pixel.x + Pixel.y) & 1) ? -1 : 1;

	// Vertical copy
	UNROLL
	for (uint r = 0; r < RADIX && Pixel.y < Size; ++r, Pixel.y += Stride)
	{
		uint Index = Pixel.y * Size + Pixel.x;
		OutDyBuffer[Index] = LocalComplexBuffer[r].x * SignCorrection * ChoppyScale; // 実数部のみコピー
	}
}

void CopyRealDataLocalToDzBuffer(in Complex LocalComplexBuffer[RADIX], uint ScanIdx, uint Loc, uint Stride, uint Size)
{
	uint2 Pixel = uint2(ScanIdx, Loc);
	// cos(pi * (m1 + m2))
	int SignCorrection = ((Pixel.x + Pixel.y) & 1) ? -1 : 1;

	// Vertical copy
	UNROLL
	for (uint r = 0; r < RADIX && Pixel.y < Size; ++r, Pixel.y += Stride)
	{
		uint Index = Pixel.y * Size + Pixel.x;
		OutDzBuffer[Index] = LocalComplexBuffer[r].x * SignCorrection; // 実数部のみコピー
	}
}

[numthreads(NUMTHREADSX, 1, 1)]
void HorizontalIFFT512x512CS(uint GroupID : SV_GroupID, uint GroupThreadID : SV_GroupThreadID)
{
	// ComplexFFTImage512x512を、float4チャンネルでなくfloat2チャンネルに直したもの

	const uint ThreadIdx = GroupThreadID;
	const uint ScanIdx  = GroupID;
	uint Head = ThreadIdx;
	const uint Stride = ARRAY_LENGTH / RADIX;

	Complex LocalComplexBuffer[RADIX];

	CopyComplexDataSrcToLocal(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH, InDkBuffer);
	GroupSharedStockhamFFT(false, LocalComplexBuffer, ARRAY_LENGTH, ThreadIdx);
	CopyComplexDataLocalToWorkBuffer(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH);
}

[numthreads(NUMTHREADSX, 1, 1)]
void DkxVerticalIFFT512x512CS(uint GroupID : SV_GroupID, uint GroupThreadID : SV_GroupThreadID)
{
	// ComplexFFTImage512x512を、float4チャンネルでなくfloat2チャンネルに直したもの

	const uint ThreadIdx = GroupThreadID;
	const uint ScanIdx  = GroupID;

	uint Head = ThreadIdx;
	const uint Stride = ARRAY_LENGTH / RADIX;

	Complex LocalComplexBuffer[RADIX];

	CopyWorkBufferToLocal(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH);
	GroupSharedStockhamFFT(false, LocalComplexBuffer, ARRAY_LENGTH, ThreadIdx);
	CopyRealDataLocalToDxBuffer(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH);
}

[numthreads(NUMTHREADSX, 1, 1)]
void DkyVerticalIFFT512x512CS(uint GroupID : SV_GroupID, uint GroupThreadID : SV_GroupThreadID)
{
	// ComplexFFTImage512x512を、float4チャンネルでなくfloat2チャンネルに直したもの

	const uint ThreadIdx = GroupThreadID;
	const uint ScanIdx  = GroupID;

	uint Head = ThreadIdx;
	const uint Stride = ARRAY_LENGTH / RADIX;

	Complex LocalComplexBuffer[RADIX];

	CopyWorkBufferToLocal(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH);
	GroupSharedStockhamFFT(false, LocalComplexBuffer, ARRAY_LENGTH, ThreadIdx);
	CopyRealDataLocalToDyBuffer(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH);
}

[numthreads(NUMTHREADSX, 1, 1)]
void DkzVerticalIFFT512x512CS(uint GroupID : SV_GroupID, uint GroupThreadID : SV_GroupThreadID)
{
	// ComplexFFTImage512x512を、float4チャンネルでなくfloat2チャンネルに直したもの

	const uint ThreadIdx = GroupThreadID;
	const uint ScanIdx  = GroupID;

	uint Head = ThreadIdx;
	const uint Stride = ARRAY_LENGTH / RADIX;   

	Complex LocalComplexBuffer[RADIX];

	CopyWorkBufferToLocal(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH);
	GroupSharedStockhamFFT(false, LocalComplexBuffer, ARRAY_LENGTH, ThreadIdx);
	CopyRealDataLocalToDzBuffer(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH);
}

StructuredBuffer<float> InDxBuffer;
StructuredBuffer<float> InDyBuffer;
StructuredBuffer<float> InDzBuffer;
RWTexture2D<float4> OutDisplacementMap;

[numthreads(8, 8, 1)]
void UpdateDisplacementMapCS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	uint2 PixelCoord = DispatchThreadId;
	uint Index = PixelCoord.y * MapSize + PixelCoord.x;

	OutDisplacementMap[PixelCoord] = float4(InDxBuffer[Index], InDyBuffer[Index], InDzBuffer[Index], 1.0);
}

float DxyzDebugAmplitude;
Texture2D<float4> InDisplacementMap;
RWTexture2D<float4> DxyzDebugTexture;

[numthreads(8, 8, 1)]
void DebugDxyzCS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	uint2 PixelCoord = DispatchThreadId;

	float3 Dxyz = InDisplacementMap[PixelCoord].xyz;
	float3 DebugColor = (Dxyz + DxyzDebugAmplitude) / 2.0 / DxyzDebugAmplitude;

	DxyzDebugTexture[PixelCoord] = float4(DebugColor.r, DebugColor.g, DebugColor.b, 1.0);
}

float PatchLength;
RWTexture2D<float4> OutGradientFoldingMap;

[numthreads(8, 8, 1)]
void GenerateGradientFoldingMapCS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	int2 PixelCoord = (int2)DispatchThreadId; // 負も扱えるようにキャスト

	int2 LeftPixelCoord = int2((PixelCoord.x - 1) % MapSize , PixelCoord.y);
	int2 RightPixelCoord = int2((PixelCoord.x + 1) % MapSize, PixelCoord.y);
	int2 UpPixelCoord = int2(PixelCoord.x, (PixelCoord.y - 1) % MapSize);
	int2 DownPixelCoord = int2(PixelCoord.x, (PixelCoord.y + 1) % MapSize);

	//TODO: Zの変位しか考慮していないNormalの計算方法である
	//マテリアル側でノイズによる変位をXYに含めた上で改めて正規化するのでここでは正規化しない
	float3 DisplaceLeft = InDisplacementMap[LeftPixelCoord].xyz;
	float3 DisplaceRight = InDisplacementMap[RightPixelCoord].xyz;
	float3 DisplaceUp = InDisplacementMap[UpPixelCoord].xyz;
	float3 DisplaceDown = InDisplacementMap[DownPixelCoord].xyz;

	float3 Normal = float3(-(DisplaceRight.z - DisplaceLeft.z), -(DisplaceDown.z - DisplaceUp.z), PatchLength / (float)MapSize * 2.0);

	// Jacobianを計算する
	float2 Dx = (DisplaceRight.xy - DisplaceLeft.xy) * ChoppyScale * (float)MapSize / PatchLength;
	float2 Dy = (DisplaceDown.xy - DisplaceUp.xy) * ChoppyScale * (float)MapSize / PatchLength;
	float Jacobian = (1.0f + Dx.x) * (1.0f + Dy.y) - Dx.y * Dy.x;
	float Folding = max(1.0f - Jacobian, 0.0f);

	OutGradientFoldingMap[PixelCoord] = float4(Normal.x, Normal.y, Normal.z, Folding);
}


uint MapWidth;
uint MapHeight;
float MeshWidth;
float MeshHeight;
float WaveLengthRow;
float WaveLengthColumn;
float Period;
float Amplitude;

[numthreads(8, 8, 1)]
void SinWaveDisplacementMap(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	const float SMALL_NUMBER = 0.0001;
	const float PI = 3.1415159;

	uint2 PixelCoord = DispatchThreadId;
	uint2 XY = PixelCoord * float2(MeshWidth, MeshHeight) / uint2(MapWidth, MapHeight);

	// Update Z only
	float Z = Amplitude * sin(2.0 * PI *
	(XY.x / max(WaveLengthRow, SMALL_NUMBER)
	+ XY.y / max(WaveLengthColumn, SMALL_NUMBER)
	+ Time / max(Period, SMALL_NUMBER))
	);
    OutDisplacementMap[PixelCoord] = float4(0.0, 0.0, Z, 1.0);
}

