#include "/Engine/Public/Platform.ush"
#include "FFT.ush"

uint Forward;
Texture2D<float4> SrcTexture;
RWTexture2D<float4> DstTexture;
uint2 SrcRectMin;
uint2 SrcRectMax;
uint4 DstRect;

// The Two-For-One trick:  FFT two real signals (f,g) of length N 
// are transformed simultaneously by treating them as a single complex signal z.
// z_n = f_n + I * g_n.
// 
// Giving the complex amplitudes (i.e. the transform)  Z_k = F_k + I * G_k.
//
// The transforms of the real signals can be recovered as
//
//  F_k = (1/2) (Z_k + ComplexConjugate(Z_{N-k}))
//  G_k = (-i/2)(Z_k - ComplexConjugate(Z_{N-k}))
//
// Furthermore, the transform of any real signal x_n has the symmetry
//  X_k = ComplexConjugate(X_{N-k})   


// Separate the FFT of two real signals that were processed togehter
// using the two-for-one trick.
//
// On input the data in LocalComplexBuffer across the threads in this group
// holds the transform  of the complex signal z = f + I * g of lenght N.
//
// On return the transform of f and g have been separated and stored
// in the LocalComplexBuffer in a form consistent with the packing the x-signal transform forward, 
// followed by the y-signal transform in reverse.
// 
// {F_o, G_o}, {F_1}, {F_2},..,{F_{N/2-1}}, {F_N/2, G_N/2}, {G_{N/2 +1}},  {G_{N/2 +2}}, .., {G_{N-2}},    {G_{N-1}} 
//     0       , 1    , 2    ,..., N/2-1,       N/2,           N/2 +1,      N/2 +2,  ..,  N/2- (2-N/2),   N/2 - (1-N/2)
// here {} := Complex
void SplitTwoForOne(inout Complex LocalComplexBuffer[RADIX], in uint Head, in uint Stride, in uint N)
{
	const uint Non2 = N / 2;
	
	// On input, Each thread has, in LocalComplexBuffer, the frequencies
	// K = Head + j * Stride;  where j = 0, .., RADIX-1.. Head = ThreadIDx, Stride = NumberOfThreads

	// Write the complex FFT into group shared memory.
	
	CopyLocalXToGroupShared(LocalComplexBuffer, Head, Stride);
	GroupMemoryBarrierWithGroupSync();

	// Construct the transform for the two real signals in the LocalComplexBuffer
	UNROLL
	for (uint i = 0, K = Head; i < RADIX; ++i, K += Stride)
	{
		//
		// K = N/2 - abs(SrcIdx - N/2)

		// DstK = SrcIdx % Non2;
		// N - k
		
		FLATTEN
		uint NmK = (K > 0) ? (N - K) : 0;

		// Z_k = LocalComplexBuffer[i]
		// If k < N/2  : Store  F_k = 1/2 (Z_k + Z*_{N-k})
		// If k > N/2  : Compute I*G_k = 1/2 (Z_k - Z*_{N-k})
		
		// Tmp =  {+,-}ComplexConjugate( Z_{N-k})
		float Tmp = SharedRealBuffer[NmK];
		Tmp *= (K > Non2) ? -1 : 1;
		LocalComplexBuffer[i].x += Tmp;
	}

	if (Head == 0)
	{
		LocalComplexBuffer[0].x = 2.0f * SharedRealBuffer[0];
	}

	GroupMemoryBarrierWithGroupSync();
	CopyLocalYToGroupShared(LocalComplexBuffer, Head, Stride);
	GroupMemoryBarrierWithGroupSync();

	UNROLL
	for (uint i = 0, K = Head; i < RADIX; ++i, K += Stride)
	{
		//
		// K = N/2 - abs(SrcIdx - N/2)

		// DstK = SrcIdx % Non2;
		// N - k
		
		FLATTEN
		uint NmK = (K > 0) ? (N - K) : 0;

		// Z_k = LocalComplexBuffer[i]
		// If k < N/2  : Store  F_k = 1/2 (Z_k + Z*_{N-k})
		// If k > N/2  : Compute I*G_k = 1/2 (Z_k - Z*_{N-k})
		
		// Tmp =  {+,-}ComplexConjugate( Z_{N-k})
		float Tmp = -SharedRealBuffer[NmK];
		
		Tmp *= (K < Non2) ? 1 : -1;

		LocalComplexBuffer[i].y += Tmp;
	}

	if (Head == 0)
	{
		LocalComplexBuffer[0].y = 2.0f * SharedRealBuffer[0];
	}

	UNROLL
	for (uint i = 0; i < RADIX; ++i)
	{
		LocalComplexBuffer[i] *= 0.5; 
	}

	UNROLL
	for (uint i = 0, K = Head; i < RADIX; ++i, K += Stride)
	{
		// If k > N/2 get G_k from I*G_k:  G_k = -I * (I G_k)
		if (K > Non2)
		{
			LocalComplexBuffer[i] = ComplexMult(Complex(0, -1), LocalComplexBuffer[i]);
		}
	}
}

// Inverse of SplitTwoForOne()
// Combine the FFT of two real signals (f,g) into a single complex signal
// for use in inverting a "two-for-one" FFT .
//
//
// On return the transform of f and g have been separated and stored
// in the LocalComplexBuffer in a form consistent with the packing the x-signal transform forward, 
// followed by the y-signal transform in reverse.
// 
// {F_o, G_o}, {F_1}, {F_2},..,{F_{N/2-1}}, {F_N/2, G_N/2}, {G_{N/2 +1}},  {G_{N/2 +2}}, .., { G_{N-2}},    {G_{N-1}} 
//     0       , 1    , 2    ,..., N/2-1,       N/2,           N/2 +1,      N/2 +2,      ..,  N/2- (2-N/2),  N/2 - (1-N/2)
// here {} := Complex
//
// On return the data in LocalComplexBuffer across the threads in this group
// hold the transform  of the complex signal z = f + ig of lenght N.
//
void MergeTwoForOne(inout Complex LocalComplexBuffer[RADIX], in uint Head, in uint Stride, in uint N)
{
	uint Non2 = N / 2;

	// Write the two FFTs into shared memory.
	float TmpX[RADIX];
	for (uint i = 0; i < RADIX; ++i)
	{
		TmpX[i] = LocalComplexBuffer[i].x;
	}

	CopyLocalYToGroupShared(LocalComplexBuffer, Head, Stride);

	GroupMemoryBarrierWithGroupSync();

	// Compose the transform of a single f+ig signal from the two real signals in the LocalComplexBuffer
	UNROLL
	for (uint i = 0, K = Head; i < RADIX; ++i, K += Stride)
	{
		// N - k 
		FLATTEN uint NmK = (K > 0) ? (N - K) : 0;

		// If k < N/2  : LocalComplexBuffer[i] = F_k,  
		//               Shared[N-K] =  G[N-K] = Conjugate(G[k]) =  (G[k]_r, -G[k]_i) = Complex(G_r, -G_i)
		//                          want   I G[k] =  float(-G[k]_i, G[k]_r)
		//                 Tmp = I * G_k   
		// If k > N/2  : LocalComplexBuffer[i] = G_k 
		//               Shared[N-K] = F[N-k] = Conjugate(F[k]) = Complex(F[k]_r, -F[k]_i)
		//                           want -I F[k] = Complex(F[k]_i, -F[k]_r)
		//                 Tmp = -I * F   
		//  ComplexConjugate( Z_{N-k})

		// Tmp = (K < Non2) ? I *  G_k : -I * F
		float Tmp = SharedRealBuffer[NmK];

		Tmp *= (K > Non2) ? -1 : 1;

		LocalComplexBuffer[i].x += Tmp;
	}

	float2 FirstElement = float2(0, SharedRealBuffer[0]);
	float2 MiddleElement = float2(0, SharedRealBuffer[Non2]);

	GroupMemoryBarrierWithGroupSync();

	// Copy TmpX to GroupShared
	UNROLL
	for (uint r = 0, i = Head; r < RADIX; ++r, i += Stride)
	{
		SharedRealBuffer[i] = TmpX[r];
	}

	GroupMemoryBarrierWithGroupSync();
	FirstElement.x = SharedRealBuffer[0];
	MiddleElement.x = SharedRealBuffer[Non2];

	UNROLL
	for (uint i = 0, K = Head; i < RADIX; ++i, K += Stride)
	{
		// N - k 
		FLATTEN uint NmK = (K > 0) ? (N - K) : 0;

		// If k < N/2  : LocalComplexBuffer[i] = F_k,  
		//               Shared[N-K] =  G[N-K] = Conjugate(G[k]) =  (G[k]_r, -G[k]_i) = Complex(G_r, -G_i)
		//                          want   I G[k] =  float(-G[k]_i, G[k]_r)
		//                 Tmp = I * G_k   
		// If k > N/2  : LocalComplexBuffer[i] = G_k 
		//               Shared[N-K] = F[N-k] = Conjugate(F[k]) = Complex(F[k]_r, -F[k]_i)
		//                           want -I F[k] = Complex(F[k]_i, -F[k]_r)
		//                 Tmp = -I * F   
		//  ComplexConjugate( Z_{N-k})

		// Tmp = (K < Non2) ? I *  G_k : -I * F
		float Tmp = SharedRealBuffer[ NmK ]; 
		Tmp *= (K > Non2) ? -1 : 1;
		
		LocalComplexBuffer[i].y += Tmp;
	}

	UNROLL
	for (uint i = 0, K = Head; i < RADIX; ++i, K += Stride)
	{
		// if k > N/2 we have G - I *  F.  Multiply by I to get F + I * G
		if (K > Non2)
		{
			LocalComplexBuffer[i] = ComplexMult(Complex(0, 1), LocalComplexBuffer[i]);
		}

		if (K == Non2)
		{
			// F_N/2 + I * G_N/2
			LocalComplexBuffer[i] = MiddleElement;
		}
	}

	if (Head == 0)
	{
		LocalComplexBuffer[0] = FirstElement;
	}
}

// The inverse of WriteTwoForOneFrequencyData()
// Reads into local registers, data written in the TwoForOneFrequency layout back into the form consistent with the transform of a single complex signal.
//const bool bIsHorizontal = true;
void ReadTwoForOneFrequencyData(inout Complex LocalComplexBuffer[2][RADIX], in uint ScanIdx, in uint Loc, in uint Stride, in uint N)
{
	const bool bIsFirstElement = (Loc == 0);
	const uint Non2 =  N / 2;

	// last two values
	float4 NValue = SrcTexture[uint2(N, ScanIdx)];
	float4 NppValue = SrcTexture[uint2(N + 1, ScanIdx)];

	uint2 Pixel = uint2(Loc, ScanIdx);
	UNROLL
	for (uint i = 0; i < RADIX; ++i, Pixel.x += Stride)
	{
		float4 SrcValue = SrcTexture[Pixel];
		LocalComplexBuffer[0][i] = SrcValue.xy;
		LocalComplexBuffer[1][i] = SrcValue.zw;

		// 以下の2ブロックの処理でパディング分の2ピクセルを埋めている

		if (Pixel.x == Non2)
		{
			// local buffer will be pure real with F_N/2,  need to add I * G_N/2 (G_N/2 is real ie float2(G_r, 0))
			LocalComplexBuffer[0][i] += NppValue.yx;
			LocalComplexBuffer[1][i] += NppValue.wz;
		}
	}

	if (bIsFirstElement)
	{
		LocalComplexBuffer[0][0] += NValue.yx;
		LocalComplexBuffer[1][0] += NValue.wz;
	}

	// Combine the transforms of the two real signals (F,G) as Z = F + I G
	MergeTwoForOne(LocalComplexBuffer[ 0 ], Loc, Stride, N);
	GroupMemoryBarrierWithGroupSync();
	MergeTwoForOne(LocalComplexBuffer[ 1 ], Loc, Stride, N);
	
	// Done with the group shared memory that was used in the merge
	GroupMemoryBarrierWithGroupSync();
}

//const bool bIsHorizontal = true;
void WriteTwoForOneFrequencyData(inout Complex LocalComplexBuffer[2][RADIX], uint ScanIdx, uint Loc, uint Stride, uint N)
{
	GroupMemoryBarrierWithGroupSync();

	// Decompose the transforms.  Note '0' and 'N/2' offsets will still be mixed, and have to be explicitly dealt with below. 
	SplitTwoForOne(LocalComplexBuffer[0], Loc, Stride, N);
	GroupMemoryBarrierWithGroupSync();
	SplitTwoForOne(LocalComplexBuffer[1], Loc, Stride, N);

	const bool bIsFirstElement = (Loc == 0);
	const uint Non2 =  N / 2;

	uint2 Pixel = uint2(Loc, ScanIdx);
	float4 DstValue;
	UNROLL
	for (uint r = 0; r < RADIX; ++r, Pixel.x += Stride)
	{
		DstValue.xy = LocalComplexBuffer[0][r];
		DstValue.zw = LocalComplexBuffer[1][r];
		DstTexture[Pixel] = DstValue;

		// 以下の2ブロックの処理でパディング分の2ピクセルを埋めている

		// The N/2 element holds F_N/2 + I G_N/2
		// Write F_N/2 into this column, and G_N/2 into the the last column 
		if (Pixel.x == Non2)
		{
			DstTexture[Pixel] = float4(DstValue.x, 0.0f, DstValue.z, 0.0f);
			DstTexture[uint2(N + 1, Pixel.y)] = float4(DstValue.y, 0.0f, DstValue.w, 0.0f);
		}
	}

	// First element holds F_o + iG_o.  
	// Write Go into the second to last column. (this is the same as G_N)
	if (bIsFirstElement)
	{
		DstValue.xy = LocalComplexBuffer[0][0];
		DstValue.zw = LocalComplexBuffer[1][0];

		DstTexture[uint2(0, Pixel.y)] = float4(DstValue.x, 0.0f, DstValue.z, 0.0f); // F_o 
		DstTexture[uint2(N, Pixel.y)] = float4(DstValue.y, 0.0f, DstValue.w, 0.0f); // G_o 
	}
}

//const bool bIsHorizontal = true;
void CopyDataSrcWindowToLocal(inout Complex LocalComplexBuffer[2][RADIX], in uint ScanIdx, uint Loc, uint Stride, uint4 Window)
{
	for (uint i = 0; i < RADIX; i++)
	{
		LocalComplexBuffer[0][i] = Complex(0.0f, 0.0f);
		LocalComplexBuffer[1][i] = Complex(0.0f, 0.0f);
	}

	// offset for window start
	uint2 Pixel = uint2(Loc, ScanIdx) + Window.xy;
	UNROLL
	for (uint i = 0; i < RADIX; ++i, Pixel.x += Stride)
	{
		bool IsWindow = !(Pixel.x > Window.z);
		if (IsWindow)
		{
			// RGBAの4チャンネルを2つの複素数で保持する
			float4 SrcValue = SrcTexture[Pixel];
			LocalComplexBuffer[0][i] = SrcValue.xy;
			LocalComplexBuffer[1][i] = SrcValue.zw;
		}
	}
}

//const bool bIsHorizontal = false;
void CopyDataSrcWindowToLocal(inout Complex LocalComplexBuffer[2][RADIX], uint ScanIdx, uint Loc, uint Stride, uint2 WindowMin, uint2 WindowMax)
{
	for (uint i = 0; i < RADIX; i++)
	{
		LocalComplexBuffer[0][i] = Complex(0.0f, 0.0f);
		LocalComplexBuffer[1][i] = Complex(0.0f, 0.0f);
	}

	uint2 Pixel = uint2(ScanIdx, Loc) + uint2(0, WindowMin.y);
	UNROLL
	for (uint i = 0; i < RADIX; ++i, Pixel.y += Stride)
	{
		bool IsWindow = !(Pixel.y > WindowMax.y);
		if (IsWindow)
		{
			// RGBAの4チャンネルを2つの複素数で保持する
			float4 SrcValue = SrcTexture[Pixel];
			LocalComplexBuffer[0][i] = SrcValue.xy;
			LocalComplexBuffer[1][i] = SrcValue.zw;
		}
	}
}

void CopyDataLocalToDstWindow(in Complex LocalComplexBuffer[2][RADIX], bool bIsHorizontal, in uint ScanIdx, uint Loc, uint Stride, uint4 ROIRect)
{
	if(bIsHorizontal)
	{
		uint2 Pixel = uint2(Loc + ROIRect.x, ScanIdx + ROIRect.y);

		UNROLL
		for (uint r = 0; r < RADIX && Pixel.x < ROIRect.z; ++r, Pixel.x += Stride)
		{
			float4 DstValue;
			DstValue.xy = LocalComplexBuffer[0][r];
			DstValue.zw = LocalComplexBuffer[1][r];

			DstTexture[Pixel] = DstValue;
		}
	}
	else
	{
		uint2 Pixel = uint2(ScanIdx + ROIRect.x, Loc + ROIRect.y);

		UNROLL
		for (uint r = 0; r < RADIX && Pixel.y < ROIRect.w; ++r, Pixel.y += Stride)
		{
			float4 DstValue;
			DstValue.xy = LocalComplexBuffer[0][r];
			DstValue.zw = LocalComplexBuffer[1][r];

			DstTexture[Pixel] = DstValue;
		}
	}
}

#if 0 // テクスチャコピー実験
[numthreads(512, 1, 1)]
#else
[numthreads(NUMTHREADSX, 1, 1)]
#endif
void TwoForOneRealFFTImage512x512(uint GroupID : SV_GroupID, uint GroupThreadID : SV_GroupThreadID)
{
#if 0 // テクスチャコピー実験
	uint2 Pixel = uint2(GroupThreadID, GroupID);
	DstTexture[Pixel] = float4(SrcTexture[Pixel].r, 0.0f, 0.0f, SrcTexture[Pixel].a);
#else
	//const bool bIsHorizontal = (TransformType & 0x1);
	//const bool bIsForward    = (TransformType & 0x2);
	const bool bIsForward = (Forward > 0);
	//const bool bModifyInput  = (TransformType & 0x4);
	const bool bIsHorizontal = true;
	//const bool bIsForward    = true;
	//const bool bModifyInput  = false;

	// Threads are defined in a 1d array.
	const uint ThreadIdx = GroupThreadID;
	// The (horizontal / vertical) scan line this thread works on
	const uint ScanIdx  = GroupID;
	// The main memory access pattern for this thread.
	uint Head = ThreadIdx;
	const uint Stride = STRIDE;   

	// Thread-local memory.  Reserve two arrays since we plit .xy and .zw channels   
	Complex LocalComplexBuffer[2][RADIX];

	if (bIsForward)
	{
		uint4 SrcRect;
		SrcRect.xy = SrcRectMin.xy;
		SrcRect.zw = SrcRectMax.xy;
		// Read from an image buffer
		CopyDataSrcWindowToLocal(LocalComplexBuffer, ScanIdx, Head, Stride, SrcRect);
	}
	else
	{
		// Read a frequency space buffer with two-for-one data layout
		ReadTwoForOneFrequencyData(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH);
	}

	// Fourier Transform  the data
	// This uses the group shared memory and has appropriate syncs
	GroupSharedFFT(bIsForward, LocalComplexBuffer, ARRAY_LENGTH, ThreadIdx);

	if (bIsForward)
	{
		// Write a frequency space buffer with two-for-one data layout
		// GroupMemoryBarrierWithGroupSync();
		WriteTwoForOneFrequencyData(LocalComplexBuffer, ScanIdx, Head, Stride, ARRAY_LENGTH);
	}
	else
	{
		// Write image space data.
		// GroupMemoryBarrierWithGroupSync();

		// This is specialized for images, where floating point errors may have
		// resulted in (very very small) negative color values.
		ScrubNANs(LocalComplexBuffer);

		CopyDataLocalToDstWindow(LocalComplexBuffer, bIsHorizontal, ScanIdx, Head, Stride, DstRect);
	}
#endif
}

[numthreads(NUMTHREADSX, 1, 1)]
void ComplexFFTImage512x512(uint GroupID : SV_GroupID, uint GroupThreadID : SV_GroupThreadID)
{
	//const bool bIsHorizontal = (TransformType & 0x1);
	//const bool bIsForward    = (TransformType & 0x2);
	const bool bIsForward = (Forward > 0);
	const bool bIsHorizontal = false;
	//const bool bIsForward    = true;

	// Threads are defined in a 1d array.
	const uint ThreadIdx = GroupThreadID;
	// The scan line this thread works on
	const uint ScanIdx  = GroupID;
	// The main memory access pattern for this thread.
	uint Head = ThreadIdx;
	const uint Stride = STRIDE;   

	// Thread-local memory.  Reserve two arrays since we plit .xy and .zw channels   
	Complex LocalComplexBuffer[2][RADIX];

	// Load the local memory from the source texture
	// LocalComplexBuffer[0][] holds .xy,  LocalComplexBuffer[1][] holds.zw

	// Read from an image buffer
	CopyDataSrcWindowToLocal(LocalComplexBuffer, ScanIdx, Head, Stride, SrcRectMin, SrcRectMax);
	//GroupMemoryBarrierWithGroupSync();

	// Fourier Transform  the data
	// This uses the group shared memory and has appropriate syncs
	GroupSharedFFT(bIsForward, LocalComplexBuffer, ARRAY_LENGTH, ThreadIdx);

	// Copy data to target buffer
	// Write image space data.
	//GroupMemoryBarrierWithGroupSync();
	CopyDataLocalToDstWindow(LocalComplexBuffer, bIsHorizontal, ScanIdx, Head, Stride, DstRect); // DstRect.MinがFIntPoint(0,0)である前提
}

